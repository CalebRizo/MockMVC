buildscript {
    ext {
        spockVersion = '1.0-groovy-2.4'
        springBootVersion = '1.3.5.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.2"
}

ext {
    snippetsDir = file('./src/docs/generated-snippets')
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'codenarc'
apply plugin: 'jacoco'

jar {
    baseName = 'gs-rest-service'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile 'com.xlson.groovycsv:groovycsv:1.1'

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile 'com.jayway.jsonpath:json-path:2.2.0'
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc:1.1.0.RELEASE') {
        exclude module: 'spring-restdocs-core'
    }
    testCompile 'org.springframework.restdocs:spring-restdocs-core:1.1.0.RELEASE'
    testCompile "org.spockframework:spock-core:${spockVersion}"
    testCompile "org.spockframework:spock-spring:${spockVersion}"
    testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testCompile 'cglib:cglib-nodep:3.1'
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    dependsOn test
    sourceDir 'src/main/asciidoc'
    attributes 'snippets': snippetsDir
}

jar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'public/apidocs'
    }
}

springBoot {
    executable = true
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}